buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "io.spring.gradle:propdeps-plugin:0.0.10.RELEASE"
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "io.franzbecker.gradle-lombok" version "2.0"
    id "net.researchgate.release" version "2.6.0"
}

ext {
    javaeeApiVersion = "8.0"
    log4jVersion = "1.2.17"
    openejbVersion = "4.7.5"
    fastjsonVersion = "1.2.49"
    eclipselinkVersion = "2.7.4"
    cxfVersion = "3.2.7"
}

configure(allprojects) {
    group = "com.harmony.umbrella"
    version = "0.0.5-SNAPSHOT"

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "propdeps"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "io.franzbecker.gradle-lombok"
    apply plugin: 'net.researchgate.release'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = "UTF-8"
    }

    repositories {
        mavenLocal()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.1.2.RELEASE"
        }
    }
}

configure(rootProject) {
    configurations.archives.artifacts.clear()
}

configure(subprojects) { subproject ->

    jar {
        manifest.attributes["Implementation-Title"] = subproject.name
        manifest.attributes["Implementation-Version"] = subproject.version
        manifest.attributes["Automatic-Module-Name"] = subproject.name.replace("-", ".")
    }

    javadoc {
        description = "Generates project-level javadoc for use in -javadoc jar"

        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.use = true
        options.encoding = "UTF-8"
        options.addStringOption("Xdoclint:none", "-quiet")

        // Suppress warnings due to cross-module @see and @link references.
        // Note that global 'api' task does display all warnings.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        classifier = "sources"
        from sourceSets.main.allSource
        // Don't include or exclude anything explicitly by default. See SPR-12085.
    }

    task javadocJar(type: Jar) {
        classifier = "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

}